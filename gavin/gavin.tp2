/*
Known Issues:
-Quest: 1. if the PC makes the quest NPCs hostile and then walks up the stairs to Valerie, the dialogue will happen as though no-one followed even if the hostile thugs followed the PC upstairs. 2. Even if the PCs betrays Gavin, the thugs remain hostile.
-When talking to his brother Jolun, Gavin will only introduce the BioWare NPCs - which can be a bit weird if there are mod NPCs in the group.


*/

/*
 * "Gavin NPC for Tutu, BGT, BG:EE, and EET"
 * Combined Tutu/BGT/BG:EE/EET installer
 * by berelinde; rtannahill@msn.com, www.gibberlings3.net
 * if you have problems with this version, feel free to contact jastey@web.de for bug reports 
*/

/* Backup folder */
BACKUP ~gavin/backup~

/* Author */
AUTHOR ~www.gibberlings3.net~ //berelinde@gmail.com

/* enable all error messages; nothing suppressed. comment this out for release version */
//MODDER

VERSION ~14~ 180312

/* launch the readme file immediately. */
README ~gavin/readme-gavin.html~

ALLOW_MISSING // allow if BG Graphics night WEDs are not installed
AR6700N.WED
FW3300N.WED
AR3300N.WED
BG3300N.WED

ALWAYS

/* check for a ready SoD */

ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN
  FAIL ~Modmerge is required before mods can be installed on this game. Check the readme for more information and a link to download Modmerge.~
END



/* These actions are processed for every component: reading of variables */

  ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN
    /* Tell the player it is using Tutu stuff */
    PRINT @0
    INCLUDE ~gavin\lib\g3_tutu_cpmvars.tpa~
    OUTER_SPRINT ~eet_var~ ~~
  END 
  ACTION_IF GAME_IS ~bgt~ THEN BEGIN
      /* Tell the player it is using BGT stuff */
      PRINT @1
      INCLUDE ~gavin\lib\g3_bgt_cpmvars.tpa~
    OUTER_SPRINT ~eet_var~ ~~
   END 
   ACTION_IF GAME_IS ~bgee~ THEN BEGIN
      /* Tell the player it is using BG:EE stuff */
      PRINT @2000
      INCLUDE ~gavin\lib\g3_bgee_cpmvars.tpa~
    OUTER_SPRINT ~eet_var~ ~~
   END 

 /* EET version! ---------------------- */

  ACTION_IF GAME_IS ~eet~ THEN BEGIN

    /* Tell the player it is using eet */
    PRINT @104 
      INCLUDE ~EET/other/cpmvars/eet_cpmvars.tpa~
      INCLUDE ~EET/other/EET_functions.tph~
  END


  /* prep tras for sound references 
  COPY ~gavin/tra/%LANGUAGE%/gavin_tmp.tra~ ~gavin/tra/%LANGUAGE%/gavin.tra~
       EVALUATE_BUFFER
  LOAD_TRA ~gavin/tra/%LANGUAGE%/gavin.tra~
*/


///////////////////////////////////////////
/* all following actions are only processed ONCE for the whole mod, independent of un- and reinstalling of single components */

ACTION_IF !FILE_EXISTS ~GAVIN/backup/gavinreinstall.mrk~ BEGIN


/* Handling of correctly formatted tra-files!! ---------------------- */

  /*
   * Copies tra files into the autotra-folder (to leave the originals untouched)
   */
  DEFINE_ACTION_FUNCTION autotra_workaround BEGIN
    COPY ~gavin/tra/english~  ~gavin/tra/autotra/%LANGUAGE%~ EVALUATE_BUFFER
    COPY ~gavin/tra/%LANGUAGE%~  ~gavin/tra/autotra/%LANGUAGE%~ EVALUATE_BUFFER
  END

  LAF autotra_workaround END


//ACTION_DEFINE_ARRAY fl#noconvert BEGIN setup END

ACTION_DEFINE_ARRAY fl#reload BEGIN gavin END

  LAF HANDLE_CHARSETS
    INT_VAR
      infer_charset = 1
    STR_VAR
      tra_path = EVAL ~gavin/Tra/autotra~
      noconvert_array = fl#noconvert
      reload_array = fl#reload
  END

COPY ~GAVIN/B!component.xx~ ~GAVIN/backup/gavinreinstall.mrk~

END //gavinreinstall.mrk



/* This again happens for every running of the installer */

   /* TRAs declared in LANGUAGE must be reloaded - by hand, because of the copying (for non-EE version */

  LOAD_TRA ~gavin/tra/autotra/%LANGUAGE%/gavin.tra~




END //ALWAYS


/* Language Settings */
AUTO_TRA ~gavin/tra/autotra/%s~

LANGUAGE ~English~ ~english~ ~gavin/tra/english/setup.tra~
LANGUAGE ~French~ ~french~ ~gavin/tra/french/setup.tra~
LANGUAGE ~Deutsch (Teildeutsche Version, der Rest ist auf Englisch)~ ~german~ ~gavin/tra/german/setup.tra~
// LANGUAGE ~Polish~ ~polish~ ~gavin/tra/polish/setup.tra~
// LANGUAGE ~Russian~ ~russian~ ~gavin/tra/russian/setup.tra~
// LANGUAGE ~Spanish~ ~spanish~ ~gavin/tra/spanish/setup.tra~
// LANGUAGE ~Latin~ ~latin~ ~gavin/tra/latin/setup.tra~
// LANGUAGE ~Klingon~ ~klingon~ ~gavin/tra/klingon/setup.tra~

/////////////////////////////////////////////
// Install GAVIN NPC                       //
/////////////////////////////////////////////
BEGIN @3
 /* Tell the player it is not Tutu, BGT, or BG:EE */
REQUIRE_PREDICATE (GAME_IS ~tutu tutu_totsc bgt bgee eet~) @2

COPY ~GAVIN/B!component.xx~ ~override/GavinNPC.B~

//BG:EE - ADD_JOURNAL
ADD_JOURNAL TITLE (@3000) @3001 @3002 @3003 @3004 USING ~gavin/tra/autotra/%LANGUAGE%/gavin.tra~

ADD_JOURNAL @3005 @3006 USING ~gavin/tra/autotra/%LANGUAGE%/gavin.tra~

ADD_JOURNAL @3007 @3008 @3009 @3010 @3011 USING ~gavin/tra/autotra/%LANGUAGE%/gavin.tra~


ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
  /* Music File .2da patching, Tutu only */
  COPY ~GAVIN/B!Blank.mus~ ~music~
  COPY_EXISTING ~songlist.2da~ ~override~
  SET_2DA_ENTRY 0 2 3 ~B!Blank.mus~
  BUT_ONLY_IF_IT_CHANGES
END

//GAVIN'S SOUNDS


  LAF HANDLE_AUDIO
    STR_VAR
      audio_path = ~gavin/audio/english~
      oggdec_path = ~gavin/audio~
  END



/* Ensures that Gavin won't leave the party because of reputation */
COPY_EXISTING ~dplayer2.bcs~ ~override~
COPY_EXISTING ~dplayer2.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~BreakingPoint()~ ~BreakingPoint()
  !CharName("Gavin",Myself)~
	END
BUT_ONLY

/* STATE.IDS patching - borrowed from BG1 NPC Project and CamDawg */
/* adds custom IsValidForPartyDialogue state */
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~
             UNLESS ~CD_STATE_NOTVALID~


/* SCS related code taken out for v9 (outdated)

*/

ACTION_IF GAME_IS ~bgt tutu tutu_totsc~ THEN BEGIN 
  /* KIT.IDS, ACTION.IDS and TRIGGER.IDS patching - borrowed in bits and pieces from BG1 NPC Project, BG2 Fixpack, and Wisp - much thanks to all */
  INCLUDE ~gavin/lib/b!ids.tph~
  INCLUDE ~gavin/lib/b!gtimes.tph~
END

/* Tutu Area Script Assignment Patching: All Areas Script ID'd */
ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
  INCLUDE ~gavin/lib/tutu_area_script_assign.tph~
END

/* Area Type Flagging - not necessary for BG:EE */
/* ToSC only: Tutu and BGT */
ACTION_IF (FILE_EXISTS_IN_GAME ~FW1500.are~) OR (FILE_EXISTS_IN_GAME ~ARW500.are~) THEN BEGIN // if TotSC is installed
  COPY_EXISTING ~%IsleofBalduranN%.are~ ~override~
                ~%IsleofBalduranS%.are~ ~override~
                ~%DurlagsTower%.are~ ~override~
                ~%Farmlands%.are~ ~override~
    READ_BYTE  "0x48" "flags"
    WRITE_BYTE "0x48" ("%flags%" BOR "0b00010001")
  BUT_ONLY_IF_IT_CHANGES
END

ACTION_IF GAME_IS ~tutu tutu_totsc bgt~ THEN BEGIN
  /* FOREST and OUTDOOR: Tutu and BGT */
COPY_EXISTING ~%FishingVillage%.are~ ~override~
              ~%Peldvale%.are~ ~override~
              ~%LionsWay%.are~ ~override~
              ~%CoastWay%.are~ ~override~
              ~%Larswood%.are~ ~override~
              ~%ShipwrecksCoast%.are~ ~override~
              ~%HighHedge%.are~ ~override~
              ~%MutaminsGarden%.are~ ~override~
              ~%Lighthouse%.are~ ~override~
              ~%RedCanyons%.are~ ~override~
              ~%SouthBeregostRoad%.are~ ~override~
              ~%Ulcaster%.are~ ~override~
              ~%ArchaeologicalSite%.are~ ~override~
              ~%FishermansLake%.are~ ~override~
              ~%NorthNashkelRoad%.are~ ~override~
              ~%LonelyPeaks%.are~ ~override~
              ~%FirewineBridge%.are~ ~override~
              ~%BearRiver%.are~ ~override~
              ~%ValleyoftheTombs%.are~ ~override~
              ~%DryadFalls%.are~ ~override~
              ~%FireLeafForest%.are~ ~override~
              ~%GibberlingMountains%.are~ ~override~
  READ_BYTE  "0x48" "flags"
  WRITE_BYTE "0x48" ("%flags%" BOR "0b00010001")
  BUT_ONLY_IF_IT_CHANGES

/* OUTDOOR ONLY: Tutu and BGT */
COPY_EXISTING ~%GnollStronghold%.are~ ~override~
              ~%NashkelMines%.are~ ~override~
              ~%FriendlyArmInn%.are~ ~override~
              ~%Temple%.are~ ~override~
              ~%NashkelCarnival%.are~ ~override~
  READ_BYTE  "0x48" "flags"
  WRITE_BYTE "0x48" ("%flags%" BOR "0b00000001")
  BUT_ONLY_IF_IT_CHANGES

/* CITY and OUTDOOR */
COPY_EXISTING ~%WyrmsCrossing%.are~ ~override~
              ~%Candlekeep_Ch6%.are~ ~override~
              ~%Gullykin%.are~ ~override~
  READ_BYTE  "0x48" "flags"
  WRITE_BYTE "0x48" ("%flags%" BOR "0b00001001")
  BUT_ONLY_IF_IT_CHANGES

END
  
//=============================================================================================
// Miloch's super-awesome code locking Beregost House 22, site of Gavin's quest
ACTION_IF GAME_IS ~tutu tutu_totsc bgee eet~ BEGIN //If Tutu or BG:EE
  OUTER_SPRINT BeregH22Reg ~Door3337~
END 
ACTION_IF GAME_IS ~bgt~ BEGIN //Otherwise (if BGT)
  OUTER_SPRINT BeregH22Reg ~Door6737~
END

COPY_EXISTING ~%Beregost%.wed~ override
              ~%Beregost%N.wed~ override
  sz = SOURCE_SIZE
  PATCH_IF sz > 0x48 BEGIN
    READ_LONG 0x8 ovr_count //Read initial offsets
    READ_LONG 0xc door_count
    READ_LONG 0x10 ovr_offset
    READ_LONG 0x14 sec_hdr
    READ_LONG 0x18 door_offset
    READ_LONG 0x1c dtc_indices
    READ_LONG sec_hdr poly_count
    READ_LONG (sec_hdr + 4) poly_offset
    READ_LONG (sec_hdr + 8) vert_offset
    READ_LONG (sec_hdr + 0xc) wall_offset
    READ_LONG (sec_hdr + 0x10) poly_table
    new_door_offset = door_offset + door_count * 0x1a
    new_vert_index = ((SOURCE_SIZE - vert_offset) / 4)

    INSERT_BYTES sz 0x20 //Insert 8 new vertex pairs
    WRITE_SHORT sz 2370 //Open door
    WRITE_SHORT (sz + 2) 1563
    WRITE_SHORT (sz + 4) 2325
    WRITE_SHORT (sz + 6) 1563
    WRITE_SHORT (sz + 8) 2325
    WRITE_SHORT (sz + 0xa) 1478
    WRITE_SHORT (sz + 0xc) 2370
    WRITE_SHORT (sz + 0xe) 1478
    WRITE_SHORT (sz + 0x10) 2370 //Closed door
    WRITE_SHORT (sz + 0x12) 1562
    WRITE_SHORT (sz + 0x14) 2330
    WRITE_SHORT (sz + 0x16) 1620
    WRITE_SHORT (sz + 0x18) 2330
    WRITE_SHORT (sz + 0x1a) 1536
    WRITE_SHORT (sz + 0x1c) 2370
    WRITE_SHORT (sz + 0x1e) 1478

    INSERT_BYTES poly_table 0x24 //Insert 2 new polygons
    WRITE_LONG poly_table new_vert_index //Starting vertex index
    WRITE_LONG (poly_table + 4) 4 //Vertex count
    WRITE_BYTE (poly_table + 8) 0x80 //Open door
    WRITE_BYTE (poly_table + 9) 0xff //Unknown
    WRITE_SHORT (poly_table + 0xa) 2325 //Boundary minimum X coordinate
    WRITE_SHORT (poly_table + 0xc) 2370 //Boundary maximum X coordinate
    WRITE_SHORT (poly_table + 0xe) 1478 //Boundary minimum Y coordinate
    WRITE_SHORT (poly_table + 0x10) 1563 //Boundary maximum Y coordinate
    WRITE_LONG (poly_table + 0x12) (new_vert_index + 4) //Starting vertex index
    WRITE_LONG (poly_table + 0x16) 4 //Vertex count
    WRITE_BYTE (poly_table + 0x1a) 0x80 //Closed door
    WRITE_BYTE (poly_table + 0x1b) 0xff //Unknown
    WRITE_SHORT (poly_table + 0x1c) 2330 //Boundary minimum X coordinate
    WRITE_SHORT (poly_table + 0x1e) 2370 //Boundary maximum X coordinate
    WRITE_SHORT (poly_table + 0x20) 1478 //Boundary minimum Y coordinate
    WRITE_SHORT (poly_table + 0x22) 1620 //Boundary maximum Y coordinate

    FOR (i = 0; i < door_count; i += 1) BEGIN //Update door polygon offsets
      WRITE_LONG (i * 0x1a + door_offset + 0x12) (THIS + 0x1a) //Open polygons offset
      WRITE_LONG (i * 0x1a + door_offset + 0x16) (THIS + 0x1a) //Closed polygons offset
      PATCH_IF (i = (door_count - 1)) BEGIN
        READ_SHORT (i * 0x1a + door_offset + 0xa) tile_index
        READ_SHORT (i * 0x1a + door_offset + 0xc) tile_count
      END
    END

    INSERT_BYTES new_door_offset 0x1a //Insert new door
    WRITE_ASCII new_door_offset ~DOOR3337~ //Name
    WRITE_SHORT (new_door_offset + 8) 1 //Closed
    WRITE_SHORT (new_door_offset + 0xa) (tile_index + tile_count) //Tile cell index
    WRITE_SHORT (new_door_offset + 0xe) 1 //Open polygons count
    WRITE_SHORT (new_door_offset + 0x10) 1 //Closed polygons count
    WRITE_LONG (new_door_offset + 0x12) (poly_table + 0x1a) //Open polygons offset
    WRITE_LONG (new_door_offset + 0x16) (poly_table + 0x1a + 0x12) //Closed polygons offset

    //Update offsets for new objects
    FOR (j = 0; j < ovr_count; j += 1) BEGIN //Update overlays
      WRITE_LONG (j * 0x18 + ovr_offset + 0x10) (THIS + 0x1a) //Tilemap offset
      WRITE_LONG (j * 0x18 + ovr_offset + 0x14) (THIS + 0x1a) //Tile index lookup offset
    END
    door_count += 1
    WRITE_LONG 0xc door_count
    WRITE_LONG 0x1c (dtc_indices + 0x1a)
    WRITE_LONG (sec_hdr + 4) (poly_offset + 0x1a)
    WRITE_LONG (sec_hdr + 8) (vert_offset + 0x1a + 0x24)
    WRITE_LONG (sec_hdr + 0xc) (wall_offset + 0x1a)
    WRITE_LONG (sec_hdr + 0x10) (poly_table + 0x1a + 0x24)
  END
BUT_ONLY

COPY_EXISTING ~%Beregost%.are~ override
  PATCH_IF SOURCE_SIZE > 0x28f BEGIN
    READ_SHORT 0x5a reg_count
    READ_LONG 0x5c reg_offset
    READ_LONG 0x7c vert_offset
    READ_SHORT 0x80 vert_count
    READ_SHORT 0x82 amb_count
    READ_LONG 0x84 amb_offset
    READ_LONG 0xa4 door_count
    READ_LONG 0xa8 door_offset
    new_door_offset = door_offset + door_count * 0xc8
    new_vert_offset = vert_offset + vert_count * 4

    FOR (h = 0; h < reg_count; h += 1) BEGIN //Adjust house travel region boundaries
      READ_ASCII (h * 0xc4 + reg_offset) reg_name //Read the name
      PATCH_IF (~%reg_name%~ STRING_EQUAL_CASE ~%BeregH22Reg%~ = 1) BEGIN //If House 22
        WRITE_SHORT (h * 0xc4 + reg_offset + 0x22) 2330 //Boundary left
        WRITE_SHORT (h * 0xc4 + reg_offset + 0x24) 1478 //Boundary top
        WRITE_SHORT (h * 0xc4 + reg_offset + 0x26) 2370 //Boundary right
        WRITE_SHORT (h * 0xc4 + reg_offset + 0x28) 1620 //Boundary bottom
        READ_LONG (h * 0xc4 + reg_offset + 0x2c) vert_index 
        last_vert_offset = (vert_offset + vert_index * 4)
        WRITE_SHORT last_vert_offset 2370
        WRITE_SHORT (last_vert_offset + 2) 1562
        WRITE_SHORT (last_vert_offset + 4) 2330
        WRITE_SHORT (last_vert_offset + 6) 1620
        WRITE_SHORT (last_vert_offset + 8) 2330
        WRITE_SHORT (last_vert_offset + 0xa) 1536
        WRITE_SHORT (last_vert_offset + 0xc) 2370
        WRITE_SHORT (last_vert_offset + 0xe) 1478
      END
    END

    FOR (i = 0; i < door_count; i += 1) BEGIN //Ascension64's awesome crash fix
      READ_ASCII (door_offset + i * 0xc8 + 0x20) door_name
      PATCH_IF (~%door_name%~ STRING_EQUAL_CASE ~door3304~ = 1) BEGIN
        READ_LONG (door_offset + i * 0xc8 + 0x48) poi_index
        READ_SHORT (door_offset + i * 0xc8 + 0x4c) poi_count
        FOR (j = poi_index; j < poi_index + poi_count; j += 1) BEGIN
          READ_SHORT (vert_offset + j * 4) x
          READ_SHORT (vert_offset + j * 4 + 2) y
          PATCH_IF (x = 243 && y = 326) BEGIN
            WRITE_SHORT (vert_offset + j * 4) 233
            WRITE_SHORT (vert_offset + j * 4 + 2) 302
          END
          PATCH_IF (x = 243 && y = 327) BEGIN
            WRITE_SHORT (vert_offset + j * 4) 233
            WRITE_SHORT (vert_offset + j * 4 + 2) 304
          END
          PATCH_IF (x = 242 && y = 325) BEGIN
            WRITE_SHORT (vert_offset + j * 4) 232
            WRITE_SHORT (vert_offset + j * 4 + 2) 304
          END
        END
      END
    END

    FOR (j = 0; j < amb_count; j += 1) BEGIN //Fix missing ambient reference
      READ_ASCII (amb_offset + j * 0xd4) amb_name
      PATCH_IF (~%amb_name%~ STRING_EQUAL_CASE ~birds~ = 1) BEGIN
        READ_SHORT (amb_offset + j * 0xd4 + 0x80) snd_count
        FOR (k = 0; k < snd_count; k += 8) BEGIN
          READ_ASCII (amb_offset + j * 0xd4 + 0x30 + k) snd_ref
          PATCH_IF (~%snd_ref%~ STRING_EQUAL_CASE ~%tutu_scripta%mb_e05c~ = 1) BEGIN
            WRITE_ASCIIE (amb_offset + j * 0xd4 + 0x30 + k) ~%tutu_scripta%MB_E05A~ #8
          END
        END
      END
    END

    INSERT_BYTES new_vert_offset 0x30 //Insert 12 new vertex pairs
    WRITE_SHORT new_vert_offset 2370 //Open door
    WRITE_SHORT (new_vert_offset + 2) 1563
    WRITE_SHORT (new_vert_offset + 4) 2325
    WRITE_SHORT (new_vert_offset + 6) 1563
    WRITE_SHORT (new_vert_offset + 8) 2325
    WRITE_SHORT (new_vert_offset + 0xa) 1478
    WRITE_SHORT (new_vert_offset + 0xc) 2370
    WRITE_SHORT (new_vert_offset + 0xe) 1478
    WRITE_SHORT (new_vert_offset + 0x10) 2370 //Closed door
    WRITE_SHORT (new_vert_offset + 0x12) 1562
    WRITE_SHORT (new_vert_offset + 0x14) 2330
    WRITE_SHORT (new_vert_offset + 0x16) 1620
    WRITE_SHORT (new_vert_offset + 0x18) 2330
    WRITE_SHORT (new_vert_offset + 0x1a) 1536
    WRITE_SHORT (new_vert_offset + 0x1c) 2370
    WRITE_SHORT (new_vert_offset + 0x1e) 1478
    WRITE_SHORT (new_vert_offset + 0x20) 147 //Open impeded cells
    WRITE_SHORT (new_vert_offset + 0x22) 128
    WRITE_SHORT (new_vert_offset + 0x24) 148
    WRITE_SHORT (new_vert_offset + 0x26) 128
    WRITE_SHORT (new_vert_offset + 0x28) 146 //Closed impeded cells
    WRITE_SHORT (new_vert_offset + 0x2a) 131
    WRITE_SHORT (new_vert_offset + 0x2c) 147
    WRITE_SHORT (new_vert_offset + 0x2e) 130

    INSERT_BYTES new_door_offset 0xc8 //Insert new door
    WRITE_ASCII new_door_offset ~Door3337~ #8 //Name
    WRITE_ASCII (new_door_offset + 0x20) ~DOOR3337~ #8 //.wed ID
    WRITE_BYTE (new_door_offset + 0x28) 0b01000010 //Flags (linked and locked)
    WRITE_LONG (new_door_offset + 0x2c) vert_count  //Open door vertex index
    WRITE_SHORT (new_door_offset + 0x30) 4 //Open door vertex count
    WRITE_SHORT (new_door_offset + 0x32) 4 //Closed door vertex count
    WRITE_LONG (new_door_offset + 0x34) (vert_count + 4) //Closed door vertex index
    WRITE_SHORT (new_door_offset + 0x38) 2325 //Open boundary minimum X coordinate
    WRITE_SHORT (new_door_offset + 0x3a) 1478 //Open boundary minimum Y coordinate
    WRITE_SHORT (new_door_offset + 0x3c) 2370 //Open boundary maximum X coordinate
    WRITE_SHORT (new_door_offset + 0x3e) 1563 //Open boundary maximum Y coordinate
    WRITE_SHORT (new_door_offset + 0x40) 2330 //Closed boundary minimum X coordinate
    WRITE_SHORT (new_door_offset + 0x42) 1478 //Closed boundary minimum Y coordinate
    WRITE_SHORT (new_door_offset + 0x44) 2370 //Closed boundary maximum X coordinate
    WRITE_SHORT (new_door_offset + 0x46) 1620 //Closed boundary maximum Y coordinate
    WRITE_LONG (new_door_offset + 0x48) (vert_count + 8) //Closed impeded vertex index
    WRITE_SHORT (new_door_offset + 0x4c) 2 //Closed impeded vertex count
    WRITE_SHORT (new_door_offset + 0x4e) 2 //Open impeded vertex count
    WRITE_LONG (new_door_offset + 0x50) (vert_count + 10) //Open impeded vertex index
    WRITE_LONG (new_door_offset + 0x68) 30 //Cursor index (door)
    WRITE_SHORT (new_door_offset + 0x74) 2342 //Trap launch X coordinate
    WRITE_SHORT (new_door_offset + 0x76) 1556 //Trap launch Y coordinate
    WRITE_ASCII (new_door_offset + 0x78) ~b!key01~ #8 //Key item
    WRITE_LONG (new_door_offset + 0x8c) 150 //Lock difficulty
    WRITE_SHORT (new_door_offset + 0x90) 2312 //Open location X coordinate
    WRITE_SHORT (new_door_offset + 0x92) 1518 //Open location Y coordinate
    WRITE_SHORT (new_door_offset + 0x94) 2312 //Closed location X coordinate
    WRITE_SHORT (new_door_offset + 0x96) 1518 //Closed location Y coordinate
    vert_count += 12
    WRITE_SHORT 0x80 vert_count
    door_count += 1
    WRITE_LONG 0xa4 door_count

    PATCH_FOR_EACH offset IN 0x7c 0x88 BEGIN
      READ_LONG offset old_value
      PATCH_IF old_value > door_offset BEGIN
        WRITE_LONG offset (old_value + 0xc8) //Update offsets to account for new door
      END
    END

    PATCH_FOR_EACH offset IN 0xa0 0xb0 0xb8 0xbc 0xc0 0xc4 0xcc BEGIN
      READ_LONG offset old_value
      PATCH_IF old_value > vert_offset BEGIN
        WRITE_LONG offset (old_value + 0xc8 + 0x30) //Update offsets to account for new door and vertices
      END
    END
  END
BUT_ONLY

COPY ~GAVIN/Items/b!key01.itm~ ~override/b!key01.itm~ //Beregost house key
  SAY NAME1 @139
  SAY NAME2 @139
  SAY UNIDENTIFIED_DESC @140
/*
Patching taken out, as the key has to be provided for BG:EE, but the bams are present.
  WRITE_BYTE 0x18 (THIS BAND 0b11111110) //Make it sellable (why not)
  WRITE_LONG 0x34 7 //Price
*/

COPY_EXISTING ~%tutu_var%inn3351.sto~ ~override~ //Let Feldepost Inn buy keys
  PATCH_IF SOURCE_SIZE > 0x9b BEGIN
    READ_LONG 0x2c bought_offset
    READ_LONG 0x30 bought_count
    found_key = 0
    found_potn = 0
    FOR (i = 0; i < bought_count; i += 1) BEGIN
      READ_LONG (bought_offset + i * 4) item_categ
      PATCH_IF item_categ = 8 BEGIN
        found_key = 1
      END
      PATCH_IF item_categ = 9 BEGIN
        found_potn = 1
        key_offset = (bought_offset + i * 4)
      END
    END
    PATCH_IF found_potn = 0 BEGIN
      key_offset = SOURCE_SIZE
    END
    PATCH_IF found_key = 0 BEGIN
      INSERT_BYTES key_offset 4
      WRITE_LONG key_offset 8
      bought_count += 1
      WRITE_LONG 0x30 bought_count
    END
  END
BUT_ONLY

RANDOM_SEED null //Initialize random placement
OUTER_SET rnk = RANDOM(1 3)
ACTION_IF rnk = 1 BEGIN
  COPY_EXISTING ~%ValleyoftheTombs_Tomb%.are~ override
    PATCH_IF SOURCE_SIZE > 0x28f BEGIN
      LPF ADD_AREA_ITEM
        INT_VAR
        container_to_add_to = 5
        STR_VAR
        item_to_add = ~b!key01~
      END
    END
  BUT_ONLY
END ELSE BEGIN
  ACTION_IF rnk = 2 BEGIN
    COPY_EXISTING ~%BanditCamp_Tent2%.are~ override
      PATCH_IF SOURCE_SIZE > 0x28f BEGIN
        LPF ADD_AREA_ITEM
          INT_VAR
          container_to_add_to = 4
          STR_VAR
          item_to_add = ~b!key01~
        END
      END
    BUT_ONLY
  END ELSE BEGIN
    COPY_EXISTING ~%UlcasterRuins%.are~ override
      PATCH_IF SOURCE_SIZE > 0x28f BEGIN
        LPF ADD_AREA_ITEM
          INT_VAR
          container_to_add_to = 4
          STR_VAR
          item_to_add = ~b!key01~
        END
      END
    BUT_ONLY
  END
END
//===============================================End of Miloch's super-awesome code======================================

/* Add locksmith in case keys don't appear */ 

ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
  EXTEND_BOTTOM ~_AR3351.bcs~ ~GAVIN/BAF/b!ar3351.baf~
    EVALUATE_BUFFER
END


ACTION_IF GAME_IS ~bgt bgee eet~ THEN BEGIN
  EXTEND_BOTTOM ~%Beregost_FeldepostsInn_L1%.bcs~ ~GAVIN/BAF/b!ar3351.baf~
    EVALUATE_BUFFER
END 

COPY ~gavin/cre/b!gavlok.cre~ ~override~ 
  SAY NAME1 @1234 
  SAY NAME2 @1234 

COMPILE ~gavin/quest/questdlg/b!gavlok.d~ 


/* Portrait Assignment */
//MKDIR ~portraits~ 

ACTION_IF GAME_IS ~bgt tutu tutu_totsc~ THEN BEGIN
COPY ~GAVIN/portraits/B!GAVINL.bmp~ ~override/bgavinL.bmp~
COPY ~GAVIN/portraits/B!GAVINS.bmp~ ~override/bgavinS.bmp~
END

ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
COPY ~GAVIN/portraits/portraits_ee/B!GAVINM.bmp~ ~override/bgavinL.bmp~
COPY ~GAVIN/portraits/portraits_ee/B!GAVINS.bmp~ ~override/bgavinS.bmp~
END

/* GAVIN'S RING */

COPY ~GAVIN/bams/b!ringgi.bam~ ~override~
COPY ~GAVIN/Items/B!SPIHIT.EFF~ ~override~
COPY ~GAVIN/Items/B!GAVRNG.ITM~ ~override~
  SAY NAME1 @64
  SAY NAME2 @64
  SAY UNIDENTIFIED_DESC @65
  SAY DESC @65

//Creature copying and naming
COPY ~Gavin/CRE/B!GAVIN.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%helm09~ #0 #0 #0 ~IDENTIFIED~ ~HELMET~
  ADD_CRE_ITEM ~%tutu_var%chan01~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~
  ADD_CRE_ITEM ~%tutu_var%shld05~ #0 #0 #0 ~IDENTIFIED~ ~SHIELD~
  ADD_CRE_ITEM ~b!gavrng~ #0 #0 #0 ~IDENTIFIED~ ~LRING~
  ADD_CRE_ITEM ~%tutu_var%hamm01~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ~%tutu_var%potn08~ #2 #0 #0 ~IDENTIFIED~ ~QITEM1~ 
  ADD_CRE_ITEM ~%tutu_var%potn20~ #0 #0 #0 ~IDENTIFIED~ ~QITEM2~
  SAY NAME1 @4
  SAY NAME2 @4
  WRITE_ASCII 0x248 ~B!GAVIN~ #8 // assign override script
  WRITE_ASCII 0x2cc ~B!GAVIN~ #8 // assign pre-joining dialogue file
  WRITE_ASCII 0x280 ~B!GAVIN~ #32 // assign DV
  SAY INITIAL_MEETING @5
  SAY MORALE @6
  SAY HAPPY @7
  SAY UNHAPPY_ANNOYED @8
  SAY UNHAPPY_SERIOUS @9
  SAY UNHAPPY_BREAKING @10
  SAY LEADER @11
  SAY TIRED @12
  SAY BORED @13
  SAY BATTLE_CRY1 @14
  SAY BATTLE_CRY2 @15
  SAY BATTLE_CRY3 @16
  SAY BATTLE_CRY4 @17
  SAY DAMAGE @18
  SAY DYING @19
  SAY HURT @20
  SAY AREA_FOREST @21
  SAY AREA_CITY @22
  SAY AREA_DUNGEON @23
  SAY AREA_DAY @24
  SAY AREA_NIGHT @25
  SAY SELECT_COMMON1 @26
  SAY SELECT_COMMON2 @27
  SAY SELECT_COMMON3 @28
  SAY SELECT_COMMON4 @29
  SAY SELECT_COMMON5 @30
  SAY SELECT_COMMON6 @31
  SAY SELECT_ACTION1 @32
  SAY SELECT_ACTION2 @33
  SAY SELECT_ACTION3 @34
  SAY SELECT_ACTION4 @35
  SAY SELECT_ACTION5 @36
  SAY SELECT_ACTION6 @37
  SAY SELECT_ACTION7 @38
  SAY SELECT_RARE1 @39
  SAY SELECT_RARE2 @40
  SAY CRITICAL_HIT @41
  SAY CRITICAL_MISS @42
  SAY TARGET_IMMUNE @43
  SAY INVENTORY_FULL @44
  SAY PICKED_POCKET @45
  SAY HIDDEN_IN_SHADOWS @46
  SAY SPELL_DISRUPTED @47
  SAY SET_A_TRAP @48
  SAY BIO @49

//Change BG2 soundset setup to BGEE
ACTION_IF GAME_IS ~bgee~ BEGIN
  COPY_EXISTING ~B!GAVIN.CRE~ ~override~
    SAY 0x1E0 @35              //  SAY BGEE_ACTION4 @35
    SAY 0x1E4 @36              //  SAY BGEE_ACTION5 @36
    SAY 0x1E8 @37              //  SAY BGEE_ACTION6 @37
    SAY 0x1EC @38              //  SAY BGEE_ACTION7 @38
    WRITE_LONG SELECT_ACTION4 (BNOT 0x0)
    WRITE_LONG SELECT_ACTION5 (BNOT 0x0)
    WRITE_LONG SELECT_ACTION6 (BNOT 0x0)
    WRITE_LONG SELECT_ACTION7 (BNOT 0x0)
  BUT_ONLY
END


//2da appending for dialogues

/* BGT, Tutu, BG:EE */

ACTION_IF GAME_IS ~bgt tutu tutu_totsc bgee~ THEN BEGIN
APPEND ~pdialog.2da~
~B!GAVIN   B!GAVINP   B!GAVINJ   B!GAVIND~
UNLESS ~B!GAVIN~

APPEND ~interdia.2da~
~B!GAVIN   BB!GAVIN~
UNLESS ~B!GAVIN~

END

/* BG1:EET */

ACTION_IF GAME_IS ~eet~ THEN BEGIN
APPEND ~bgdialog.2da~
~B!GAVIN   B!GAVINP   B!GAVINJ   B!GAVIND~
UNLESS ~B!GAVIN~

APPEND ~bgbanter.2da~
~B!GAVIN   BB!GAVIN~
UNLESS ~B!GAVIN~

/* SOD */
APPEND ~bdbanter.2da~
~B!GAVIN   BB!GAVIN~
UNLESS ~B!GAVIN~
END


/* SoD */

ACTION_IF (GAME_IS ~bgee eet~ AND FILE_EXISTS_IN_GAME ~bd0103.are~) THEN BEGIN
APPEND ~bddialog.2da~
~B!GAVIN   B!GAVINP   B!GAVINJ   B!GAVIND~
UNLESS ~B!GAVIN~

END



/* Initiate all Gavin's .dlg files */
COMPILE ~GAVIN\dlg\B!initiatefiles.d~

//Dialogue compilation
COMPILE EVALUATE_BUFFER ~GAVIN/DLG/BB!GAVIN.d~
COMPILE EVALUATE_BUFFER ~GAVIN/DLG/B!Gavin.d~
COMPILE EVALUATE_BUFFER ~GAVIN/DLG/B!GavinJ.d~
COMPILE EVALUATE_BUFFER ~GAVIN/DLG/B!GAVINP.d~
COMPILE EVALUATE_BUFFER ~GAVIN/DLG/B!JOLUN.d~
COMPILE EVALUATE_BUFFER ~GAVIN/DLG/b!addtalks.d~


/* fix for Joseph's Greenstone Ring ending up in Gavin's inventory, not with Joseph's widow:
widow needs a script name: */

COPY_EXISTING ~%tutu_scriptbg%FTOWN2%eet_var%.cre~ ~override~
  WRITE_EVALUATED_ASCII 0x280 ~%tutu_scriptbg%FTOWN2%eet_var%~ #32 //DV

/* now Gavin has to give her the ring (he does it himself, good lad!) */

COPY_EXISTING ~B!GavinJ.dlg~ ~override~
DECOMPILE_AND_PATCH BEGIN
REPLACE_TEXTUALLY ~\(TakePartyItem("[_]RINGJOS")\)~ ~\1 GiveItem("%tutu_var%RINGJOS","%tutu_scriptbg%FTOWN2%eet_var%")~
END
BUT_ONLY_IF_IT_CHANGES

/* for EE. need a different check, because this file doesn't get carried over? */
ACTION_IF (FILE_EXISTS_IN_GAME ~override/X#BG1NPCPhase1.G3~ OR FILE_EXISTS_IN_GAME ~override/X#KABAND.DLG~)THEN BEGIN // if BG1 NPC is installed
  COMPILE EVALUATE_BUFFER ~GAVIN/DLG/B!X#KABAND.d~
END

ACTION_IF GAME_IS ~tutu_totsc bgt bgee eet~ THEN BEGIN// if TotSC is installed
  COMPILE EVALUATE_BUFFER ~GAVIN/DLG/B!GavinJ_ToSC.d~
  COMPILE EVALUATE_BUFFER ~GAVIN/DLG/b!gavin_tosc_new.d~
  COMPILE EVALUATE_BUFFER ~GAVIN/DLG/B!Evie.d~
END

/* script compilation */
COMPILE EVALUATE_BUFFER ~GAVIN/BAF/B!GAVIN.baf~
COMPILE EVALUATE_BUFFER ~GAVIN/BAF/B!JOLUN.baf~

ACTION_IF GAME_IS ~tutu_totsc bgt bgee eet~ THEN BEGIN// if TotSC is installed
  EXTEND_BOTTOM  ~B!GAVIN.bcs~ ~GAVIN/BAF/B!GAVIN_ToSC.baf~
        EVALUATE_BUFFER
  COMPILE EVALUATE_BUFFER ~GAVIN/BAF/B!EVIE.baf~
  COMPILE EVALUATE_BUFFER ~GAVIN/BAF/B!DARLOS.baf~
END

COPY_EXISTING ~%tutu_var%prism.cre~ ~override/B!JOLUN.cre~
  SAY NAME1 @50
  SAY NAME2 @50
  WRITE_ASCII 0x248 ~B!JOLUN~ #8 // override
  WRITE_ASCII 0x2cc ~B!JOLUN~ #8 // dialogue
  WRITE_ASCII 0X34 ~bjoluns~ #8 //small portrait
  WRITE_ASCII 0x280 ~B!JOLUN~ #32 //DV
SAY INITIAL_MEETING @51
SAY DAMAGE @52
SAY DYING @53
SAY SELECT_COMMON1 @51
SAY DIALOGUE_DEFAULT @51

/* adding Darlos and Evie */
ACTION_IF GAME_IS ~tutu_totsc bgt bgee eet~ THEN BEGIN// if TotSC is installed
  COPY ~GAVIN/CRE/B!DARLOS.CRE~ ~override~
  SAY NAME1 @54
  SAY NAME2 @54
  WRITE_ASCII 0x248 ~B!DARLOS~ #8 // override
  WRITE_ASCII 0x2cc ~B!DARLOS~ #8 // dialogue
  WRITE_ASCII 0X34 ~bdarlos~ #8 //small portrait
  WRITE_ASCII 0x280 ~B!DARLOS~ #32 //DV
  SAY INITIAL_MEETING @55
  SAY DAMAGE @56
  SAY DYING @57
  SAY SELECT_COMMON1 @55
  SAY DIALOGUE_HOSTILE @58
  SAY DIALOGUE_DEFAULT @55

  COPY ~GAVIN/CRE/B!EVIE.CRE~ ~override~
  SAY NAME1 @59
  SAY NAME2 @59
  WRITE_ASCII 0x248 ~B!EVIE~ #8 // override
  WRITE_ASCII 0x2cc ~B!EVIE~ #8 // dialogue
  WRITE_ASCII 0X34 ~bevie~ #8 //small portrait
  WRITE_ASCII 0x280 ~B!EVIE~ #32 //DV
  SAY INITIAL_MEETING @60
  SAY DAMAGE @61
  SAY DYING @62
  SAY SELECT_COMMON1 @60
  SAY DIALOGUE_HOSTILE @63
  SAY DIALOGUE_DEFAULT @60
END

/* interjection script compilation */
EXTEND_BOTTOM ~B!GAVIN.bcs~ ~GAVIN/BAF/B!GavinInt.baf~
  EVALUATE_BUFFER

/* friendship script compilation */
EXTEND_BOTTOM ~B!GAVIN.bcs~ ~GAVIN/BAF/B!GavinFriendship.baf~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~B!GAVIN.bcs~ ~GAVIN/BAF/B!GavinFriendBreaks.baf~
  EVALUATE_BUFFER

//Script extension
ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
  EXTEND_BOTTOM ~_AR3300.bcs~ ~GAVIN/BAF/B!FW3300.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~_AR3400.bcs~ ~GAVIN/BAF/B!FW3400.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~_AR0800.bcs~ ~GAVIN/BAF/B!FW0800.baf~
    EVALUATE_BUFFER
END


ACTION_IF GAME_IS ~bgt bgee eet~ THEN BEGIN
  EXTEND_BOTTOM ~%Beregost%.bcs~ ~GAVIN/BAF/B!FW3300.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%Temple%.bcs~ ~GAVIN/BAF/B!FW3400.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%EBaldursGate%.bcs~ ~GAVIN/BAF/B!FW0800.baf~
    EVALUATE_BUFFER
END

/* Unlock door Door1007 */
COPY_EXISTING ~%UlgothsBeard%.are~ ~override~
  GET_OFFSET_ARRAY doors 0xa8 4 0xa4 4 0 0 0xc8
  PHP_EACH doors AS i => r BEGIN
    READ_ASCII r+0x20 id (8)
    PATCH_IF ~%id%~ STRING_EQUAL_CASE ~door1007~ BEGIN
      WRITE_BYTE r+0x28 THIS & `BIT1
    END
  END
BUT_ONLY


ACTION_IF GAME_IS ~tutu_totsc~ THEN BEGIN// if TotSC is installed
  EXTEND_BOTTOM ~_AR1000.bcs~ ~GAVIN/BAF/B!FW1000.baf~
    EVALUATE_BUFFER
END

ACTION_IF GAME_IS ~bgt bgee eet~ THEN BEGIN// if TotSC is installed
  EXTEND_BOTTOM ~%UlgothsBeard%.bcs~ ~GAVIN/BAF/B!FW1000.baf~
    EVALUATE_BUFFER
END

//dream script compilation
COMPILE EVALUATE_BUFFER ~GAVIN/BAF/B!GAVIND.baf~

//GAVIN'S QUEST

/* Script Compilation */

  /* extending Gavin's script */
    EXTEND_BOTTOM ~B!GAVIN.bcs~ ~GAVIN/Quest/QuestBAF/B!Quest.baf~
      EVALUATE_BUFFER
    COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestBAF/B!VALCUT.baf~

  /* creature placement */

  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestBAF/B!GUSTAV.baf~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestBAF/B!PENNY.baf~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestBAF/B!BERTRA.baf~
    USING ~GAVIN/TRA/autotra/%LANGUAGE%/b!bertrabaf.tra~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestBAF/B!VALER.baf~
    USING ~GAVIN/TRA/autotra/%LANGUAGE%/b!valerbaf.tra~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestBAF/B!THUG1.baf~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestBAF/B!THUG2.baf~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestBAF/B!THUG3.baf~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestBAF/B!MESNGR.baf~

  EXTEND_BOTTOM ~%tutu_var%SLEEPMH.bcs~ ~GAVIN/Quest/QuestBAF/B!SLEEPMH.baf~
      EVALUATE_BUFFER
  EXTEND_BOTTOM ~%tutu_var%SLEEPFH.bcs~ ~GAVIN/Quest/QuestBAF/B!SLEEPFH.baf~
      EVALUATE_BUFFER

/* Creatures */

/* Compiling the dialog files */

  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestDLG/B!QUEST.d~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestDLG/B!GUSTAV.d~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestDLG/B!PENNY.d~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestDLG/B!BERTRA.d~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestDLG/B!VALER.d~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestDLG/B!THUG1.d~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestDLG/B!THUG2.d~
  COMPILE EVALUATE_BUFFER ~Gavin/Quest/QuestDLG/B!THUG3.d~
  COMPILE EVALUATE_BUFFER ~GAVIN/Quest/QuestDLG/B!RUMOR.d~


/* Getting rid of the occupants of the quest area */
ACTION_IF GAME_IS ~tutu tutu_totsc bgt bgee~ THEN BEGIN
COPY_EXISTING ~%tutu_var%SLEEPMH.CRE~ ~override~
  SAY NAME1 @66
  SAY NAME2 @66
  WRITE_EVALUATED_ASCII 0x250 ~%tutu_var%SLEEPMH~ #8 // class
//  WRITE_EVALUATED_ASCII 0x280 ~%tutu_var%SLEEPMH~ #32 //DV
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~%tutu_var%SLEEPFH.CRE~ ~override~
  SAY NAME1 @67
  SAY NAME2 @67
  WRITE_EVALUATED_ASCII 0x250 ~%tutu_var%SLEEPFH~ #8 // class
//  WRITE_EVALUATED_ASCII 0x280 ~%tutu_var%SLEEPFH~ #32 //DV
  BUT_ONLY_IF_IT_CHANGES
END

/* this does probably the same but it's newer syntax */
ACTION_IF GAME_IS ~eet~ THEN BEGIN
COPY_EXISTING ~SLEEPMH.CRE~ ~override~
	WRITE_ASCII SCRIPT_CLASS ~SLEEPMH~ #8
//	WRITE_ASCII DEATHVAR ~SLEEPMH~ #32
BUT_ONLY

COPY_EXISTING ~SLEEPFH.CRE~ ~override~
	WRITE_ASCII SCRIPT_CLASS ~SLEEPFH~ #8
//	WRITE_ASCII DEATHVAR ~SLEEPFH~ #32
BUT_ONLY
END


//Creature copying and naming
COPY ~Gavin/Quest/QuestCRE/B!PENNY.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%leat01~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~
  ADD_CRE_ITEM ~%tutu_var%boot02~ #0 #0 #0 ~IDENTIFIED~ ~BOOTS~
  ADD_CRE_ITEM ~%tutu_var%SW1H14~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ~%tutu_var%potn14~ #0 #0 #0 ~IDENTIFIED~ ~QITEM1~
  SAY NAME1 @68
  SAY NAME2 @68
  WRITE_ASCII 0x248 ~B!PENNY~ #8 // override
  WRITE_ASCII 0x2cc ~B!PENNY~ #8 // dialogue
  WRITE_ASCII 0x280 ~B!PENNY~ #32 //DV
  SAY INITIAL_MEETING @69
  SAY DAMAGE @70
  SAY DYING @71
  SAY SELECT_COMMON1 @69

COPY ~Gavin/Quest/QuestCRE/B!GUSTAV.CRE~ ~override~
  SAY NAME1 @72
  SAY NAME2 @72
  WRITE_ASCII 0x248 ~B!GUSTAV~ #8 // override
  WRITE_ASCII 0x2cc ~B!GUSTAV~ #8 // dialogue
  WRITE_ASCII 0x280 ~B!GUSTAV~ #32 //DV
  SAY INITIAL_MEETING @73
  SAY DAMAGE @74
  SAY DYING @75
  SAY SELECT_COMMON1 @73
  SAY DIALOGUE_DEFAULT @73

COPY ~Gavin/Quest/QuestCRE/B!BERTRA.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%helm09~ #0 #0 #0 ~IDENTIFIED~ ~HELMET~
  ADD_CRE_ITEM ~%tutu_var%plat04~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~
  ADD_CRE_ITEM ~%tutu_var%SHLD06~ #0 #0 #0 ~IDENTIFIED~ ~SHIELD~
  ADD_CRE_ITEM ~%tutu_var%ring12~ #0 #0 #0 ~IDENTIFIED~ ~LRING~
  ADD_CRE_ITEM ~%tutu_var%HAMM02~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ~%tutu_var%potn08~ #2 #0 #0 ~IDENTIFIED~ ~QITEM1~
  ADD_CRE_ITEM ~%tutu_var%misc32~ #4 #0 #0 ~IDENTIFIED~ ~INV1~
  ADD_CRE_ITEM ~%tutu_var%misc31~ #3 #0 #0 ~IDENTIFIED~ ~INV2~
  SAY NAME1 @76
  SAY NAME2 @76
  WRITE_ASCII 0x248 ~B!BERTRA~ #8  // override
  WRITE_ASCII 0x2cc ~B!BERTRA~ #8  // dialogue
  WRITE_ASCII 0x280 ~B!BERTRA~ #32 //DV
/*
  WRITE_ASCII 0x250 ~~ #8          //class
  WRITE_ASCII 0x268 ~~ #8          // default
*/
  SAY INITIAL_MEETING @77
  SAY BATTLE_CRY1 @77
  SAY BATTLE_CRY2 @77
  SAY BATTLE_CRY3 @77
  SAY BATTLE_CRY4 @77
  SAY BATTLE_CRY5 @77
  SAY DAMAGE @78
  SAY DYING @79
  SAY SELECT_COMMON1 @77
  SAY DIALOGUE_DEFAULT @77

COPY ~Gavin/Quest/QuestCRE/B!VALER.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%clck17~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~
  ADD_CRE_ITEM ~%tutu_var%ring06~ #0 #0 #0 ~IDENTIFIED~ ~LRING~
  ADD_CRE_ITEM ~%tutu_var%STAF08~ #0 #0 #0 ~identified&unstealable&undroppable~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ~%tutu_var%scrl77~ #1 #1 #0 ~IDENTIFIED~ ~QITEM1~
  ADD_CRE_ITEM ~%tutu_var%scrl1y~ #1 #1 #0 ~IDENTIFIED~ ~QITEM2~
  ADD_CRE_ITEM ~%tutu_var%misc42~ #0 #0 #0 ~IDENTIFIED~ ~INV1~
  ADD_CRE_ITEM ~%tutu_var%misc44~ #0 #0 #0 ~IDENTIFIED~ ~INV2~
  ADD_CRE_ITEM ~%tutu_var%misc41~ #2 #0 #0 ~IDENTIFIED~ ~INV3~
  ADD_CRE_ITEM ~%tutu_var%STAF02~ #2 #0 #0 ~identified&unstealable~ ~INV4~
  SAY NAME1 @80
  SAY NAME2 @80
  WRITE_ASCII 0x248 ~B!VALER~ #8 // override
  WRITE_ASCII 0x2cc ~B!VALER~ #8 // dialogue
  WRITE_ASCII 0x280 ~B!VALER~ #32 //DV
/*
  WRITE_ASCII 0x250 ~~ #8          //class
  WRITE_ASCII 0x268 ~~ #8          // default
*/
  SAY INITIAL_MEETING @81
  SAY BATTLE_CRY1 @82
  SAY BATTLE_CRY2 @82
  SAY BATTLE_CRY3 @82
  SAY BATTLE_CRY4 @82
  SAY BATTLE_CRY5 @82
  SAY DAMAGE @83
  SAY DYING @84
  SAY SELECT_COMMON1 @81

COPY ~Gavin/Quest/QuestCRE/B!THUG1.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%leat04~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~
  ADD_CRE_ITEM ~%tutu_var%AX1H01~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ~%tutu_var%AX1H01~ #0 #0 #0 ~IDENTIFIED~ ~SHIELD~
  SAY NAME1 @85
  SAY NAME2 @85
  WRITE_ASCII 0x248 ~B!THUG1~ #8 // override
  WRITE_ASCII 0x2cc ~B!THUG1~ #8 // dialogue
  WRITE_ASCII 0x280 ~B!THUG1~ #32 //DV
  SAY INITIAL_MEETING @86
  SAY BATTLE_CRY1 @86
  SAY BATTLE_CRY2 @86
  SAY BATTLE_CRY3 @86
  SAY BATTLE_CRY4 @86
  SAY BATTLE_CRY5 @86
  SAY DAMAGE @87
  SAY DYING @88
  SAY SELECT_COMMON1 @86
  SAY DIALOGUE_DEFAULT @86

COPY ~Gavin/Quest/QuestCRE/B!THUG2.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%leat04~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~
  ADD_CRE_ITEM ~%tutu_var%AX1H01~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ~%tutu_var%AX1H01~ #0 #0 #0 ~IDENTIFIED~ ~SHIELD~
  SAY NAME1 @89
  SAY NAME2 @89
  WRITE_ASCII 0x248 ~B!THUG2~ #8 // override
  WRITE_ASCII 0x2cc ~B!THUG2~ #8 // dialogue
  WRITE_ASCII 0x280 ~B!THUG2~ #32 //DV
  SAY INITIAL_MEETING @90
  SAY BATTLE_CRY1 @90
  SAY BATTLE_CRY2 @90
  SAY BATTLE_CRY3 @90
  SAY BATTLE_CRY4 @90
  SAY BATTLE_CRY5 @90
  SAY DAMAGE @91
  SAY DYING @92
  SAY SELECT_COMMON1 @93
  SAY SELECT_COMMON2 @94
  SAY DIALOGUE_DEFAULT @93

COPY ~Gavin/Quest/QuestCRE/B!THUG3.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%leat04~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~
  ADD_CRE_ITEM ~%tutu_var%AX1H01~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ~%tutu_var%AX1H01~ #0 #0 #0 ~IDENTIFIED~ ~SHIELD~
  SAY NAME1 @95
  SAY NAME2 @95
  WRITE_ASCII 0x248 ~B!THUG3~ #8 // override
  WRITE_ASCII 0x2cc ~B!THUG3~ #8 // dialogue
  WRITE_ASCII 0x280 ~B!THUG3~ #32 //DV
  SAY INITIAL_MEETING @90
  SAY BATTLE_CRY1 @90
  SAY BATTLE_CRY2 @90
  SAY BATTLE_CRY3 @90
  SAY BATTLE_CRY4 @90
  SAY BATTLE_CRY5 @90
  SAY DAMAGE @91
  SAY DYING @92
  SAY SELECT_COMMON1 @93
  SAY SELECT_COMMON2 @94
  SAY DIALOGUE_DEFAULT @93

COPY_EXISTING ~%tutu_var%SERVAN.CRE~ ~override/B!MESNGR.CRE~
  SAY NAME1 @96
  SAY NAME2 @96
  WRITE_ASCII 0x248 ~B!MESNGR~ #8 // override
  WRITE_ASCII 0x2cc ~B!MESNGR~ #8 // dialogue
  WRITE_ASCII 0x280 ~B!MESNGR~ #32 //DV
  SAY INITIAL_MEETING @97
  SAY DAMAGE @56
  SAY DYING @57
  SAY SELECT_COMMON1 @97
  SAY DIALOGUE_DEFAULT @97

/* Miscellaneous */

COMPILE EVALUATE_BUFFER ~GAVIN/DLG/B!Courtesans.d~

EXTEND_BOTTOM ~%tutu_scriptp%ROSBA_B.bcs~ ~GAVIN/BAF/B!ROSBA_B.baf~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~%tutu_scriptp%ROSBA_C.bcs~ ~GAVIN/BAF/B!ROSBA_C.baf~
  EVALUATE_BUFFER


COPY_EXISTING ~%tutu_scriptp%ROSBA_B.CRE~ ~override~
  SAY NAME1 @98
  SAY NAME2 @98
  WRITE_EVALUATED_ASCII 0x248 ~%tutu_scriptp%ROSBA_B~ #8 // override
  WRITE_EVALUATED_ASCII 0x280 ~%tutu_scriptp%ROSBA_B~ #32 //DV
  WRITE_ASCII 0X34 ~BKittyS~ #8 //small portrait
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~%tutu_scriptp%ROSBA_C.CRE~ ~override~
  SAY NAME1 @98
  SAY NAME2 @98
  WRITE_EVALUATED_ASCII 0x248 ~%tutu_scriptp%ROSBA_C~ #8 // override
  WRITE_EVALUATED_ASCII 0x280 ~%tutu_scriptp%ROSBA_C~ #32 //DV
  WRITE_ASCII 0X34 ~BWrenS~ #8 //small portrait
  BUT_ONLY_IF_IT_CHANGES


/* SoD, EET and stuff */
/* Gavin will have "good bye" StringHeads in SoD, too */

ACTION_IF FILE_EXISTS_IN_GAME ~bd0120.bcs~ THEN BEGIN
EXTEND_BOTTOM ~bd0120.bcs~ ~GAVIN/BAF/b!_bd0120.baf~
  EVALUATE_BUFFER

EXTEND_BOTTOM ~bd0130.bcs~ ~GAVIN/BAF/b!_bd0120.baf~
  EVALUATE_BUFFER

/* Gavin will go after Korlasz is deteated */
EXTEND_TOP ~bd0103.bcs~ ~GAVIN/BAF/b!_bd0103.baf~
  EVALUATE_BUFFER
END



ACTION_IF GAME_IS ~eet~ THEN BEGIN
/* EET: add Gavin to the ToB Fate Spirit (the BG1 Gavin won't be summoned, though) */

LAF ~EET_NPC_TRANSITION~
  INT_VAR
    type = 1
  STR_VAR
    dv = "B!GAVIN"
    override_BG1 = "B!GAVIN"
    string = "@1235"
    stringPosDV = "Garrick"
END
END

/* BGT: Erase journal entries upon transition to BGII */
ACTION_IF GAME_IS ~bgt~ THEN BEGIN
  EXTEND_TOP ~AR0602.bcs~ ~gavin/baf/AR0602_bgt.baf~ EVALUATE_BUFFER
END



/////////// ROMANCE //////////////////

BEGIN @99
  GROUP @100

REQUIRE_COMPONENT ~Gavin.tp2~ 0 @101
REQUIRE_FILE ~override/GavinNPC.B~ @101

  /* Gavin's romance timer */
PRINT @102

PRINT @103

OUTER_SPRINT ~gavintimer~ ~placeholder_value~
OUTER_WHILE (!(IS_AN_INT ~gavintimer~) OR (~gavintimer~ > 0x5) OR (~gavintimer~ < 0x1)) BEGIN
  PRINT @105
  ACTION_READLN ~gavintimer~
END
      ACTION_IF ("gavintimer" = 1) THEN BEGIN
      OUTER_SET GAVIN_TIMER = 3600
      PRINT @106
      END
      ACTION_IF ("gavintimer" = 2) THEN BEGIN
      OUTER_SET GAVIN_TIMER = 2700
      PRINT @107
      END
      ACTION_IF ("gavintimer" = 3) THEN BEGIN
      OUTER_SET GAVIN_TIMER = 1800
      PRINT @108
      END
      ACTION_IF ("gavintimer" = 4) THEN BEGIN
      OUTER_SET GAVIN_TIMER = 900
      PRINT @109
      END
      ACTION_IF ("gavintimer" = 5) THEN BEGIN
      OUTER_SET GAVIN_TIMER = 5400
      PRINT @110
      END

COPY ~GAVIN/Romance/B!romance.B~ ~override/GavinRom.B~

/* Compile dialog */

  COMPILE EVALUATE_BUFFER ~GAVIN/Romance/RomanceDLG/B!GavinLovetalks.d~
  COMPILE EVALUATE_BUFFER ~GAVIN/Romance/RomanceDLG/GavinNPCRomBanters.d~
  COMPILE EVALUATE_BUFFER ~GAVIN/Romance/RomanceDLG/AjantisPCReGavin.d~
  COMPILE EVALUATE_BUFFER ~GAVIN/Romance/B!REED/B!REED.d~

/* Extend script */
  COMPILE EVALUATE_BUFFER ~GAVIN/Romance/B!REED/B!REED.baf~

  EXTEND_TOP ~B!GAVIN.bcs~  ~GAVIN/Romance/RomanceBAF/B!GavinRomance.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~B!GAVIN.bcs~  ~GAVIN/Romance/RomanceBAF/B!GAVINRomBreaks.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~B!GAVIN.bcs~  ~GAVIN/Romance/RomanceBAF/B!GAVINRomNPC.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%AJANTIS_BCS%.bcs~     ~GAVIN/Romance/RomanceBAF/AjantisPCRegardingGavin.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%CORAN_BCS%.bcs~     ~GAVIN/Romance/RomanceBAF/GavinCoranRomComments.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%XAN_BCS%.bcs~     ~GAVIN/Romance/RomanceBAF/GavinXanRomComments.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%ALORA_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Alora.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%BRANWEN_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Branwen.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%DYNAHEIR_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Dynaheir.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%EDWIN_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Edwin.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%ELDOTH_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Eldoth.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%FALDORN_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Faldorn.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%GARRICK_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Garrick.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%IMOEN_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Imoen.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%JAHEIRA_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Jaheira.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%KAGAIN_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Kagain.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%KHALID_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Khalid.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%KIVAN_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Kivan.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%MINSC_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Minsc.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%MONTARON_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Montaron.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%QUAYLE_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Quayle.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%SAFANA_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Safana.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%SHARTEEL_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!SharTeel.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%SKIE_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Skie.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%TIAX_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Tiax.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%VICONIA_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Viconia.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%XZAR_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Xzar.baf~
    EVALUATE_BUFFER
  EXTEND_BOTTOM ~%YESLICK_BCS%.bcs~   ~GAVIN/Romance/RomanceBAF/B!Yeslick.baf~
    EVALUATE_BUFFER

/* Engagement Ring */
COPY ~GAVIN/bams/b!ringpi.bam~ ~override~
COPY ~GAVIN/Items/b!pcrin1.EFF~ ~override~
COPY ~GAVIN/Items/b!pcrin2.EFF~ ~override~
COPY ~GAVIN/Items/b!pcrin3.EFF~ ~override~
COPY ~GAVIN/Items/b!pcrin4.EFF~ ~override~
COPY ~GAVIN/Items/b!pcrin5.EFF~ ~override~
COPY ~GAVIN/Items/b!pcrin6.EFF~ ~override~
COPY ~GAVIN/Items/b!pcrin7.EFF~ ~override~
COPY ~GAVIN/Items/b!pcring.ITM~ ~override~
  SAY NAME1 @111
  SAY NAME2 @111
  SAY UNIDENTIFIED_DESC @112
  SAY DESC @113

COPY ~GAVIN/bams/b!handi.bam~ ~override~
COPY ~GAVIN/bams/b!handd.bam~ ~override~

COPY ~GAVIN/Items/B!HAND.itm~ ~override~
  SAY NAME1 @114
  SAY NAME2 @114
  SAY UNIDENTIFIED_DESC @115
  SAY DESC @115

COPY ~GAVIN/bams/b!fingd.bam~ ~override~

COPY ~GAVIN/bams/b!fingg.bam~ ~override~
COPY ~GAVIN/bams/b!fingi.bam~ ~override~
COPY ~GAVIN/Items/B!FINGER.itm~ ~override~
  SAY NAME1 @116
  SAY NAME2 @116
  SAY UNIDENTIFIED_DESC @117
  SAY DESC @117

COPY ~GAVIN/Items/B!GRING2.itm~ ~override~
  SAY NAME1 @64
  SAY NAME2 @64
  SAY UNIDENTIFIED_DESC @118
  SAY DESC @118

COPY ~GAVIN/Romance/B!REED/B!REED.CRE~ ~override~
  ADD_CRE_ITEM ~%tutu_var%clck15~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~
  ADD_CRE_ITEM ~%tutu_var%ring06~ #0 #0 #0 ~IDENTIFIED~ ~LRING~
  ADD_CRE_ITEM ~%tutu_var%ring02~ #0 #0 #0 ~IDENTIFIED~ ~RRING~
  ADD_CRE_ITEM ~%tutu_var%dagg05~ #50 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ~%tutu_var%dagg03~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON2~
  ADD_CRE_ITEM ~%tutu_var%potn08~ #2 #0 #0 ~IDENTIFIED~ ~QITEM1~
  ADD_CRE_ITEM ~%tutu_var%wand04~ #0 #0 #0 ~IDENTIFIED~ ~QITEM2~
  ADD_CRE_ITEM ~%tutu_var%QITEM26~ #10 #0 #0 ~IDENTIFIED~ ~INV2~
  SAY NAME1 @119
  SAY NAME2 @119
  WRITE_ASCII 0x248 ~B!REED~ #8 // override
  WRITE_ASCII 0x2cc ~B!REED~ #8 // dialogue
  WRITE_ASCII 0X34 ~breeds~ #8 //small portrait
  WRITE_ASCII 0x280 ~B!REED~ #32 //DV
  SAY INITIAL_MEETING @81
  SAY DAMAGE @61
  SAY DYING @62
  SAY SELECT_COMMON1 @81
  SAY DIALOGUE_DEFAULT @81

COPY ~GAVIN/Items/b!gavbk.itm~ ~override/b!gavbk.itm~
  SAY NAME1 @121
  SAY NAME2 @121
  SAY UNIDENTIFIED_DESC @122
  SAY DESC @122

COPY_EXISTING ~%tutu_var%scrltar.itm~ ~override/b!gavscl.itm~
  SAY NAME1 @123
  SAY NAME2 @123
  SAY UNIDENTIFIED_DESC @124
  SAY DESC @124

BEGIN @125
GROUP @100
  REQUIRE_FILE ~override/GavinRom.B~ @126
    EXTEND_BOTTOM ~B!GAVIND.bcs~  ~GAVIN/Flirt/GavinFlirtD.baf~
      EVALUATE_BUFFER
    EXTEND_BOTTOM ~B!GAVIN.bcs~  ~GAVIN/Flirt/GavinFlirtsbaf.baf~
      EVALUATE_BUFFER
    COMPILE EVALUATE_BUFFER ~GAVIN/Flirt/gavinflirts.d~

BEGIN @127
  REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~FW0100.are~) @128
  REQUIRE_FILE ~override/GavinRom.B~ @126

/* Tutu only; detects and adjusts speeds if Tutufix speed adjustment component is installed. */
/* Always next to last in the tp2 */

  COPY_EXISTING ~_MONTAR.cre~ ~override~
    READ_BYTE  0x33 "eff_version"
    READ_LONG  0x2c4 "effects_offset"
    READ_LONG  0x2c8 "fx_num"
    SET "adjust_speed" = 0
    PATCH_IF ("%eff_version%" = 0) BEGIN
      FOR (loops = 0 ; loops < fx_num ; loops = loops + 1) BEGIN
        READ_SHORT ("%effects_offset%" + ("%loops%" * 0x30)) "type"
        PATCH_IF (("%type%" = 176) OR ("%type%" = 126)) BEGIN
          SET "loops" = "%fx_num%"
          SET "adjust_speed" = 1
        END
      END
    END ELSE BEGIN
      FOR (loops = 0 ; loops < fx_num ; loops = loops + 1) BEGIN
        READ_LONG ("%effects_offset%" + 8 + ("%loops%" * 264)) "type"
        PATCH_IF (("%type%" = 176) OR ("%type%" = 126)) BEGIN
          SET "loops" = "%fx_num%"
          SET "adjust_speed" = 1
        END
      END
    END
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~B!GAVIN.CRE~ ~override~
    PATCH_IF ("%adjust_speed%" = 1) BEGIN
      READ_BYTE  0x33  "eff_version"
      READ_LONG  0x2b0 "known_offset"
      READ_LONG  0x2b8 "slots_offset"
      READ_LONG  0x2bc "items_offset"
      READ_LONG  0x2c4 "effects_offset"
      READ_LONG  0x2c8 "#effects"
      WRITE_LONG 0x2c8 ("%#effects%" + 1)
      PATCH_IF ("%eff_version%" = 0) BEGIN
        PATCH_IF ("%known_offset%" >= "%effects_offset%") BEGIN
          WRITE_LONG 0x2b0 ("%known_offset%" + 48)
        END
        PATCH_IF ("%slots_offset%" >= "%effects_offset%") BEGIN
          WRITE_LONG 0x2b8 ("%slots_offset%" + 48)
        END
        PATCH_IF ("%items_offset%" >= "%effects_offset%") BEGIN
          WRITE_LONG 0x2bc ("%items_offset%" + 48)
        END
        INSERT_BYTES  ("%effects_offset%"     ) 48
          WRITE_SHORT ("%effects_offset%"     ) 176
          WRITE_BYTE  ("%effects_offset%" +  2) 1 // target=self
          WRITE_LONG  ("%effects_offset%" +  4) 0xfffffffd // walking speed
          WRITE_LONG  ("%effects_offset%" +  8) 0 // modifier type inc/dec
          WRITE_BYTE  ("%effects_offset%" + 12) 9 // timing mode permanent
          WRITE_BYTE  ("%effects_offset%" + 18) 100
      END ELSE BEGIN
        PATCH_IF ("%known_offset%" >= "%effects_offset%") BEGIN
          WRITE_LONG 0x2b0 ("%known_offset%" + 264)
        END
        PATCH_IF ("%slots_offset%" >= "%effects_offset%") BEGIN
          WRITE_LONG 0x2b8 ("%slots_offset%" + 264)
        END
        PATCH_IF ("%items_offset%" >= "%effects_offset%") BEGIN
          WRITE_LONG 0x2bc ("%items_offset%" + 264)
        END
        INSERT_BYTES  ("%effects_offset%"     ) 264
          WRITE_LONG  ("%effects_offset%" +  8) 176
          WRITE_LONG  ("%effects_offset%" + 12) 1
          WRITE_LONG  ("%effects_offset%" + 20) 0xfffffffd
          WRITE_LONG  ("%effects_offset%" + 24) 0
          WRITE_LONG  ("%effects_offset%" + 28) 9
          WRITE_SHORT ("%effects_offset%" + 36) 100
      END
    END
    BUT_ONLY_IF_IT_CHANGES

/*
BEGIN @129
  SUBCOMPONENT @130
  REQUIRE_FILE ~override/GavinNPC.B~ @131
   ACTION_IF GAME_IS ~bgt tutu tutu_totsc~ THEN BEGIN
    COPY ~GAVIN/portraits/B!GAVINL.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/B!GAVINS.bmp~ ~override/bgavinS.bmp~
   END
   ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
    COPY ~GAVIN/portraits/portraits_ee/B!GAVINM.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/portraits_ee/B!GAVINS.bmp~ ~override/bgavinS.bmp~
   END
*/
BEGIN @136
  SUBCOMPONENT @130
  REQUIRE_FILE ~override/GavinNPC.B~ @131
   ACTION_IF GAME_IS ~bgt tutu tutu_totsc~ THEN BEGIN
    COPY ~GAVIN/portraits/AMGAVM.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/AMGAVS.bmp~ ~override/bgavinS.bmp~
   END
   ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
    COPY ~GAVIN/portraits/portraits_ee/AMGAVM.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/portraits_ee/AMGAVS.bmp~ ~override/bgavinS.bmp~
   END
BEGIN @137
  SUBCOMPONENT @130
  REQUIRE_FILE ~override/GavinNPC.B~ @131
   ACTION_IF GAME_IS ~bgt tutu tutu_totsc~ THEN BEGIN
    COPY ~GAVIN/portraits/b!gavin4l.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/b!gavin4s.bmp~ ~override/bgavinS.bmp~
   END
   ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
    COPY ~GAVIN/portraits/portraits_ee/b!gavin4m.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/portraits_ee/b!gavin4s.bmp~ ~override/bgavinS.bmp~
   END
BEGIN @138
  SUBCOMPONENT @130
  REQUIRE_FILE ~override/GavinNPC.B~ @131
   ACTION_IF GAME_IS ~bgt tutu tutu_totsc~ THEN BEGIN
    COPY ~GAVIN/portraits/b!gavin5l.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/b!gavin5s.bmp~ ~override/bgavinS.bmp~
   END
   ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
    COPY ~GAVIN/portraits/portraits_ee/b!gavin5m.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/portraits_ee/b!gavin5s.bmp~ ~override/bgavinS.bmp~
   END
BEGIN @132
  SUBCOMPONENT @130
  REQUIRE_FILE ~override/GavinNPC.B~ @131
   ACTION_IF GAME_IS ~bgt tutu tutu_totsc~ THEN BEGIN
    COPY ~GAVIN/portraits/B!Gavin1L.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/B!Gavin1S.bmp~ ~override/bgavinS.bmp~
   END
   ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
    COPY ~GAVIN/portraits/portraits_ee/B!Gavin1m.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/portraits_ee/B!Gavin1S.bmp~ ~override/bgavinS.bmp~
   END
BEGIN @133
  SUBCOMPONENT @130
    REQUIRE_FILE ~override/GavinNPC.B~ @131
   ACTION_IF GAME_IS ~bgt tutu tutu_totsc~ THEN BEGIN
    COPY ~GAVIN/portraits/B!Gavin2L.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/B!Gavin2S.bmp~ ~override/bgavinS.bmp~
   END
   ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
    COPY ~GAVIN/portraits/portraits_ee/B!Gavin2m.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/portraits_ee/B!Gavin2S.bmp~ ~override/bgavinS.bmp~
   END
BEGIN @2003 
  SUBCOMPONENT @130
    REQUIRE_FILE ~override/GavinNPC.B~ @131
   ACTION_IF GAME_IS ~bgt tutu tutu_totsc~ THEN BEGIN
    COPY ~GAVIN/portraits/b_gav1M.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/b_gav1S.bmp~ ~override/bgavinS.bmp~
   END
   ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
    COPY ~GAVIN/portraits/portraits_ee/b_gav1M.bmp~ ~override/bgavinL.bmp~
    COPY ~GAVIN/portraits/portraits_ee/b_gav1S.bmp~ ~override/bgavinS.bmp~
   END



/////////////////////////////////
/////////// Install PID /////////
/////////////////////////////////

/* Always keep last in the tp2 */

BEGIN @134
  REQUIRE_FILE ~override/GavinNPC.B~ @135
    COMPILE EVALUATE_BUFFER ~GAVIN/PID/PIDnight.d~
    COMPILE EVALUATE_BUFFER ~GAVIN/PID/B!GavinPID.d~
    EXTEND_BOTTOM ~B!GAVIND.bcs~ ~GAVIN/PID/PIDinitD.baf~
      EVALUATE_BUFFER
